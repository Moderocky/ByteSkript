
on load:
    trigger:
        print "Hello, welcome to ByteSkript!"
        print "What's your name?"
        set {name} to system input
        print "Nice to meet you, " + {name} + "!"
        wait 1 second
        print "Here is a little tour of ByteSkript's feature set."
        wait 1 second
        print "Let's start with functions."
        run first_func("arguments.")
        wait 1 second
        print "ByteSkript supports simple timings and wait-effects."
        print "ByteSkript will also wait while expecting system input..."
        set {var} to system input
        print "Thank you for writing '" + {var} + "' :)"
        wait 1 second
        print "Simple maths is also supported."
        set {var} to 1 + 1
        print "1 + 1 = " + {var}
        run common_types()
        wait 1 second
        run io_stuff()
        run section_things(true)
        wait 2 seconds
        exit the program

function section_things(boolean):
    trigger:
        print "ByteSkript has various types of section."
        if {boolean} is true:
            print "This line is inside an if-section!"
        else:
            print "This line will never be run."
        wait 1 second
        set {runner} to a new runnable:
            print "This line comes from a runnable!"
            print "It was created earlier, but is being run now."
            wait 100 milliseconds
            print "Timings are supported in runnables too."
        print "Special runnables are also supported."
        run {runner} async
        run a new runnable async:
            print "This runnable is being run at the same time!"

function common_types:
    trigger:
        print "The current script is called " + the current script
        wait 1 second
        print "Lists are supported as a variable type."
        set {list} to a new list
        print "Our list is empty, what do you want to add to it?"
        add system input to {list}
        print "Great! I added a cabbage."
        add "Cabbage" to {list}
        print "Our list now contains: " + {list}
        set {size} to size of {list}
        print "That's " + {size} + " things!!!"

function io_stuff:
    trigger:
        print "Simple I/O things are also supported."
        set {file} to a new file at "test.txt"
        print "I've made a new file called " + {file}
        print "What would you like to write in it?"
        set the contents of {file} to system input
        print "Great! Our file now says: " + the contents of {file}
        wait 1 second
        print "I don't like our file anymore... :("
        delete {file}
        print "I deleted it."

function return_values:
    trigger:
        return "return values."

function first_func(thing):
    trigger:
        print "Functions can be called from each other."
        print "They can be given " + {thing}
        print "They can even provide " + return_values()
